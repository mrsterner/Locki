import net.fabricmc.loom.task.RemapJarTask

plugins {
	id 'fabric-loom' version '1.9-SNAPSHOT'
	id 'io.github.ladysnake.chenille' version '0.13.1'
	id 'maven-publish'
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

sourceSets {
	dummy
}

chenille {
	configureTestmod {
		withBaseTestRuns()
	}

	license = 'LGPL'
	javaVersion = 21
}

repositories {
	mavenCentral()
	mavenLocal()
	chenille.repositories {
		modrinth()
		ladysnake()
		lucko()
		jitpack()
		terraformers()
		shedaniel()
	}
	maven {
		name = 'Quilt Snapshots'
		url = 'https://maven.quiltmc.org/repository/snapshot'
	}
	maven {
		url = "https://maven.siphalor.de/"
		content {
			includeGroup 'de.siphalor'
		}
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

	modIncludeImplementation ("org.ladysnake.cardinal-components-api:cardinal-components-base:${cca_version}") { transitive = false }
	modIncludeImplementation ("org.ladysnake.cardinal-components-api:cardinal-components-entity:${cca_version}") { transitive = false }
	modIncludeImplementation "me.lucko:fabric-permissions-api:${fpa_version}"

	compileOnly "com.demonwav.mcdev:annotations:1.0"

	modCompileOnly "maven.modrinth:backslot:${backslot_version}"
	modLocalRuntime ("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") { transitive = false }

	modLocalRuntime ("com.terraformersmc:modmenu:${modmenu_version}") { transitive = false }
	modLocalImplementation("org.ladysnake:elmendorf:${elmendorf_version}") { transitive = false }

	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
	testImplementation project(":junit-loader")

	annotationProcessor(sourceSets.dummy.output)
}

test {
	useJUnitPlatform()

	maxHeapSize = '1G'
}

processResources {
	inputs.property "version", project.version

	filesMatching("quilt.mod.json") {
		expand "version": project.version
	}
}

java {
	withSourcesJar()
}

jar {
	from("COPYING.LESSER") {
		rename { "LICENSE_${project.archivesBaseName}"}
	}
}

remapJar {
	addNestedDependencies.set(false)
}

tasks.register("standaloneJar", Jar) {
	from sourceSets.main.output
	archiveClassifier.set('standalone-dev')
}

tasks.register("remapStandaloneJar", RemapJarTask) {
	dependsOn(project.standaloneJar)
	archiveClassifier.set('standalone')
	inputFile.set(standaloneJar.archiveFile.get())
	addNestedDependencies.set(true)
}

tasks.named("assemble") {
	dependsOn(remapStandaloneJar)
}

chenille {
	configurePublishing {
		mainArtifact = remapStandaloneJar.archiveFile
		withLadysnakeMaven()
		withCurseforgeRelease()
		withModrinthRelease()
		withGithubRelease()
	}
}

modrinth {
	loaders = ['quilt']
}
